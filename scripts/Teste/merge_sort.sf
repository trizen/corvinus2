#!/usr/bin/ruby

# Merge sort algorithm

func merge(left, right) {

    var array = [];
    var nL = left.len;
    var nR = right.len;

    var(i=0, j=0);
    while (i<nL && j<nR) {
        array.append(left[i] <= right[j] ? left[i++] : right[j++]);
    }

    array += left.ft(i, left.end);
    array += right.ft(j, right.end);

    return array;
}

func merge_sort(array) {
    var n = array.len;
    n < 2 && return;

    var mid   = (n/2 -> int);
    var left  = array.ft(0, mid-1);
    var right = array.ft(mid, array.end);

    __FUNC__(left);
    __FUNC__(right);

    array.@[array.range] = merge(left, right)...;
}

var arr = [3,4,7,6,2,5,1,0].shuffle;
merge_sort(arr);

assert_eq(arr, arr.sort);
say "** Test passed!";
