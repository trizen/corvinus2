#!/usr/bin/ruby

#
## Inspired by Tom Hudson: http://www.youtube.com/watch?v=GjGRhIl0xWs
#

enum (
    PUSH,
    ADD,
    PRINT,
    HALT,
    JMPLT,
);

struct Op {
    Text name,
    Numar nargs,
}

var ops = Dict(
    PUSH,    Op("push", 1),
    ADD,     Op("add", 0),
    PRINT,   Op("print", 0),
    HALT,    Op("halt", 0),
    JMPLT,   Op("jmplt", 2),
);

clasa VM (
    code = [],
    pc = 0,
    stack = [],
    sp = -1
){

    metoda trace {
        var op = ops{code[pc]};
        var args = code.ft(pc+1, pc + op.nargs);
        printf("%04d:%-10s%20s%40s\n", pc, op.name, args, stack);
    }

    metoda run(c=[]) {
        code = c;

        bucla {

            # Trace
            self.trace();

            # Fetch
            var op = code[pc++];

            # Decode
            dat(op) {
                cand (PUSH) {
                    var val = code[pc++];
                    stack[++sp] = val;
                }
                cand (ADD) {
                    var a = stack[sp--];
                    var b = stack[sp--];

                    stack[++sp] = a+b;
                }
                cand (PRINT) {
                    var val = stack[sp--];
                    say val;
                }
                cand (JMPLT) {
                    var lt = code[pc++];
                    var addr = code[pc++];

                    if (stack[sp] < lt) {
                        pc = addr;
                    }
                }
                cand (HALT) {
                    return;
                }
            }
        }
    }
}

var code = [
    PUSH, 2,
    PUSH, 3,
    ADD,
    JMPLT, 10, 2,
    PRINT,
    HALT,
];

var v = VM();
v.run(code);
